# Maintainer: Your Name <frontear4594@gmail.com>
pkgname=sdbus-cpp-git
pkgver=1.3.0.r4.gcfb71bd
pkgrel=1
pkgdesc="High-level C++ D-Bus library for Linux designed to provide easy-to-use yet powerful API in modern C++"
arch=("x86_64")
url="https://github.com/Kistler-Group/sdbus-cpp"
license=(
    "LGPL-2.1"
    "custom:LGPL-Exception"
)
depends=(
    "expat"
    "systemd-libs"
)
makedepends=(
    "cmake"
    "git"
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
replaces=("${pkgname%-git}")
source=("${pkgname%-git}::git+${url}.git")
md5sums=("SKIP")

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$srcdir/${pkgname%-git}"
    cmake -B build \
        -DBUILD_CODE_GEN=ON \
        -DBUILD_DOC=OFF \
        -DBUILD_TESTS=OFF \
        -DBUILD_LIBSYSTEMD=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=ON \
        -DBUILD_EXAMPLES=OFF
}

build() {
    cd "$srcdir/${pkgname%-git}"
    cmake --build build
}

package() {
    cd "$srcdir/${pkgname%-git}"
    cmake --install build --prefix "${pkgdir}/usr"
    install -Dm0644 -t "${pkgdir}/usr/share/licenses/${pkgname}" ./COPYING-LGPL-Exception
}
