# Maintainer: Ali Rizvi <frontear4594@gmail.com>
pkgname="libimobiledevice-git"
pkgver=1.3.0.r190.g6fc41f5
pkgrel=1
pkgdesc="A cross-platform protocol library to access iOS devices"
arch=("x86_64")
url="https://github.com/libimobiledevice/libimobiledevice"
license=(
    "GPL2"
    "LGPL2.1"
)
depends=(
    "libimobiledevice-glue-git"
    "libplist-git"
    "libusbmuxd-git"
    "openssl"
)
makedepends=("git")
optdepends=(
    "doxygen: for building documentation"
    "cython0: for python bindings"
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
replaces=("${pkgname%-git}")
source=("${pkgname%-git}::git+${url}.git")
md5sums=("SKIP")

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --long --tags --abbrev=7 | sed "s/-/.r/;s/-/./"
}

build() {
    cd "$srcdir/${pkgname%-git}"
    ./autogen.sh --prefix=/usr
    make
}

package() {
    cd "$srcdir/${pkgname%-git}"
    make DESTDIR="$pkgdir/" install
}
