# Maintainer: Ali Rizvi <frontear4594@gmail.com>
pkgname=aylurs-gtk-shell-git
pkgver=1.4.0.r30.gdc3439f
pkgrel=1
pkgdesc="A customizable and extensible shell"
arch=("x86_64")
url="https://github.com/Aylur/ags"
license=("GPL3")
depends=(
    "gjs"
    "glib2"
    "gtk-layer-shell"
    "libpulse" # forced dependency of audio service
)
makedepends=(
    "git"
    "gobject-introspection"
    "meson"
    "npm"
    "typescript"
)
optdepends=(
    "gnome-bluetooth-3.0: For functionality of the provided Bluetooth service"
    "libdbusmenu-gtk3: For functionality of the provided SystemTray service"
    "networkmanager: For functionality of the provided Network service"
    "upower: For functionality of the provided Battery service"
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
replaces=("${pkgname%-git}")
source=("${pkgname%-git}::git+${url}")
md5sums=("SKIP")

pkgver() {
	cd "$srcdir/${pkgname%-git}"
    git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
    git submodule update --init
    npm install
}

build() {
	cd "$srcdir/${pkgname%-git}"
    arch-meson build --libdir "lib/${pkgname%-git}" # prevent libgvc.so from clashing with other packages
    meson compile -C build
}

package() {
	cd "$srcdir/${pkgname%-git}"
    meson install -C build --destdir "${pkgdir}"
}
