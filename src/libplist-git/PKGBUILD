# Maintainer: Ali <frontear4594@gmail.com>
pkgname="libplist-git"
pkgver=2.3.0.r19.gd45396a
pkgrel=1
pkgdesc="A library to handle Apple Property List format in binary or XML"
arch=("x86_64")
url="https://github.com/libimobiledevice/libplist"
license=(
    "GPL2"
    "LGPL2.1"
)
depends=()
makedepends=("git" "python")
optdepends=(
    "doxygen: for building documentation"
    "cython0: for python bindings"
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
replaces=("${pkgname%-git}")
source=("${pkgname%-git}::git+${url}.git")
md5sums=("SKIP")

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --long --tags --abbrev=7 | sed "s/-/.r/;s/-/./"
}

build() {
    cd "$srcdir/${pkgname%-git}"
    ./autogen.sh --prefix=/usr
    make
}

package() {
    cd "$srcdir/${pkgname%-git}"
    make DESTDIR="$pkgdir/" install
}
